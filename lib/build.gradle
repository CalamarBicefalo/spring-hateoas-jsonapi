plugins {
    id 'java'
    id 'war'
    id 'jacoco'
    id 'io.freefair.lombok' version '5.1.0'
    id 'org.asciidoctor.jvm.convert' version '3.1.0'
    id 'maven-publish'
}

group = 'com.toedter'
version = '0.5.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/release' }
}

configurations {
    docs
}

dependencies {
    implementation 'org.springframework.hateoas:spring-hateoas:1.1.0.RELEASE'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
    implementation 'org.atteo:evo-inflector:1.2.2'

    testImplementation 'org.slf4j:slf4j-api:1.7.30'
    testImplementation 'org.slf4j:jcl-over-slf4j:1.7.30'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.springframework:spring-test:5.2.6.RELEASE'
    testImplementation 'org.springframework.hateoas:spring-hateoas:1.1.0.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.springframework:spring-webmvc:5.2.6.RELEASE'
    testImplementation 'javax.persistence:javax.persistence-api:2.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux:2.3.1.RELEASE'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    docs "io.spring.docresources:spring-doc-resources:0.2.2.RELEASE@zip"
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    finalizedBy jacocoTestReport
}

jar {
    archivesBaseName = 'spring-hateoas-jsonapi'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
    afterEvaluate {
        getClassDirectories().setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/Jackson2JsonApiModule\$**Mixin*', '**/AbstractJsonApiSerializer**'])
        }))
    }
}

task doc(type: Sync) {
    dependsOn asciidoctor
    dependsOn javadoc

    from "$buildDir/docs/javadoc"
    into "$buildDir/docs/asciidoc/javadoc"
}

task prepareAsciidocBuild(type: Sync) {
    dependsOn configurations.docs
    from {
        configurations.docs.collect { zipTree(it) }
    }
    from "src/main/asciidoc/"
    into "$buildDir/asciidoc/build"
}

asciidoctor {
    sourceDir "$buildDir/asciidoc/build"
    baseDirFollowsSourceFile()

    sources {
        include '*.adoc'
    }

    resources {
        from(sourceDir) {
            include 'images/*', 'css/**', 'js/**'
        }
    }

    attributes 'version': "${project.version}",
            'docinfo': 'shared',
            'stylesdir': 'css/',
            'stylesheet': 'spring.css',
            'linkcss': true,
            'icons': 'font'
}

asciidoctor.dependsOn prepareAsciidocBuild

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'spring-hateoas-jsonapi'
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv('OSSRH_USERNAME')
                password System.getenv('OSSRH_PASSWORD')
            }
            if (project.version.endsWith("-SNAPSHOT")) {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            } else {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            }
        }
    }
}

// we don't publish the gradle module meta data
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
